Sqrt:
  bit floatmode,(iy+ModeFlags2)
  jr nz,floatsqrt
  ld a,(hl)
  inc hl
  cp $AE
  push af
  jr nz,+_
  ld (parsePtr),hl
_:
  call ParseFullArg
  ld hl,sqrtHL
  ld (next_page_call_address),hl
  ld h,b
  ld l,c
  call next_page_call

  ld (ThetaPrimeVar),hl
  ld c,a
  ld b,0
  pop af
  ret nz
  ;if HL<=BC, increment BC
  sbc hl,bc
  ret c
  ret z
  inc bc
  ret
floatsqrt:
  ld hl,sqrtSingle
  jr float_stepin_1
sinefloat:
  ld hl,sinSingle
  jr float_stepin_1
_:
  ld hl,cosSingle
float_stepin_1:
  push hl     ;the call to make
  call ParseFullArg
float_stepin_1_:
  push bc     ;the second argument
  call floatstackpush
  ld b,h
  ld c,l
  pop de
  pop hl
  ld (next_page_call_address),hl
  ex de,hl
  jp next_page_call
Cos:
  bit floatmode,(iy+ModeFlags2)
  jr nz,-_
  ld a,64
  .db $FE
Sin:
  bit floatmode,(iy+ModeFlags2)
  jr nz,sinefloat
  call ParseFullArg
  xor a
  add a,c
Sine:
  ld d,a
  rlca
  rlca
  sbc a,a
  xor d
  and 63
  ld b,0
#if (sinTable&255)<192
#if (sintable&255)==0
  ld h,sinTable>>8
  ld l,a
#else
  ld hl,sinTable
  add a,l
  ld l,a
#endif
#else
  ld c,a
  ld hl,sinTable
  add hl,bc
#endif
  ld c,(hl)
  rlc d
  jr c,NegateBC
  ret
_:
  ld hl,absSingle
  jp float_stepin_1
absToken:
  bit floatmode,(iy+ModeFlags2)
  jr nz,-_
  call ParseFullArg
  bit 7,b
  ret z
  jr negateBC
Negative:
  bit floatmode,(iy+ModeFlags2)
  jr nz,+_
  call ParseFullArg
negateBC:
  xor a \ sub c \ ld c,a
  sbc a,a \ sub b \ ld b,a
  ret
_:
  ld hl,negSingle
  jp float_stepin_1
logToken:
    bit floatmode,(iy+ModeFlags2)
    jp z,ErrBadToken
_:
    call ParseNextFullArg_HL
    cp 2Bh
    ld hl,logSingle
    jp z,float_stepin_2_
    ld hl,log10Single
    push hl
    jr float_stepin_1_
Add:
  push bc
  bit floatmode,(iy+ModeFlags2)
  jr nz,+_
  call ParseFullArg
  pop hl
  xor a
  add hl,bc
  ld b,h
  ld c,l
  ld h,a
  adc a,a
  ld l,a
  ld (ThetaPrimeVar),hl
  ret
_:
  ld hl,addSingle
  jp float_stepin
SubtractToken:
  push bc
  bit floatmode,(iy+ModeFlags2)
  jr nz,+_
  call ParseFullArg
  pop hl
  xor a
  sbc hl,bc
  ld b,h
  ld c,l
  ld h,a
  ld l,a
  ld (ThetaPrimeVar),hl
  ret
_:
  ld hl,subSingle
  jp float_stepin
Cubed:
  ld d,b
  ld e,c
  call DE_Times_BC
  ex de,hl
  jr mul_stepin
Squared:
  ld d,b
  ld e,c
  jr mul_stepin
Multiply:
  push bc
  bit floatmode,(iy+ModeFlags2)
  jr nz,+_
  call ParseFullArg
  pop de
mul_stepin:
  call DE_Times_BC
  ld b,h
  ld c,l
  ld (ThetaPrimeVar),de
  ret
_:
  ld hl,mulSingle
float_stepin:
  push hl
  call ParseFullArg
  push bc
  call floatstackpush
  ld b,h
  ld c,l
  pop de
  pop hl
  ld (next_page_call_address),hl
  pop hl
  jp next_page_call
floatstackpush:
  ld hl,(floatstack_ptr)
  ld bc,4
  add hl,bc
  ld (floatstack_ptr),hl
  ld a,l \ cp floatstack_top&255
  ret nz
#if (floatstack_top>>8)!=(floatstack_bottom>>8)
  ld a,h \ cp floatstack_top>>8
  ret nz
#endif
  ld hl,floatstack_bottom
  ld (floatstack_ptr),hl
  ret
Comment:
SkipLine:
  dec hl
  bit floatmode,(iy+ModeFlags2)
  jp nz,ConvRStr_Float
  call EndOfLine
  jp ParseArg2
Divide:
  push bc
  bit floatmode,(iy+ModeFlags2)
  jr nz,+_
  call ParseFullArg
  pop hl
  call HL_Div_BC
  ld b,h
  ld c,l
  ld (ThetaPrimeVar),de
  ret
_:
  ld hl,divSingle
  jp float_stepin
PowToken:
  bit floatmode,(iy+ModeFlags2)
  jp z,ErrBadTOken
  push bc
  ld hl,powSingle
  jp float_stepin

_:
  ld hl,expSingle
  jp float_stepin_1
PowerOf2:
  bit floatmode,(iy+ModeFlags2)
  jr nz,-_
  call ParseFullArg
  ld a,c
  ld hl,0
  ld d,h
  ld e,h
  cp 32
  jr nc,Po2End
  inc l
  or a
  jr z,Po2End
  ld b,a
_:
  add hl,hl
  rl e
  rl d
  djnz -_
Po2End:
  ld b,h
  ld c,l
  ret

Logic:
Equals:
  bit floatmode,(iy+ModeFlags2)
  jr nz,feq
  push bc
factorialStepIn:
  call ParseFullArg
  pop hl
  xor a
  sbc hl,bc
  ld b,a
  ld c,a
  ret nz
  inc c
  ret
feq:
  call fcmp
  ld bc,0
  ret nz
  inc c
  ret
fcmp:
  push bc
  call ParseNextFullArg
  ld hl,cmpSingle
  ld (next_page_call_address),hl
  pop hl
  ld d,b
  ld e,c
  jp next_page_call
moreLogic:
  push af
  push bc
  call ParseFullArg
  pop hl
  pop af
  ret
notLogic:
  call MoreLogic
  ld a,b \ cpl \ ld b,a
  ld a,c \ cpl \ ld c,a
  ret
andLogic:
  call MoreLogic
  ld a,h \ and b \ ld b,a
  ld a,l \ and c \ ld c,a
  ret
orLogic:
  call MoreLogic
  ld a,h \ or b \ ld b,a
  ld a,l \ or c \ ld c,a
  ret
xorLogic:
  call MoreLogic
  ld a,h \ xor b \ ld b,a
  ld a,l \ xor c \ ld c,a
  ret
Less:
  bit floatmode,(iy+ModeFlags2)
  jr nz,flt
  call MoreLogic
  xor a
  sbc hl,bc
  ld b,a
  adc a,a
  ld c,a
  ret
flt:
  call fcmp
  ld bc,0
  ret nc
  inc c
  ret
Greater:
  bit floatmode,(iy+ModeFlags2)
  jr nz,fgt
  call MoreLogic
  xor a
  scf
  sbc hl,bc
  ccf
  ld b,a
  adc a,a
  ld c,a
  ret
fgt:
  call fcmp
  ld bc,0
  ret z
  ret c
  inc c
  ret
LessOrEqual:
  bit floatmode,(iy+ModeFlags2)
  jr nz,fle
  call MoreLogic
  xor a
  scf
  sbc hl,bc
  ld b,a
  adc a,a
  ld c,a
  ret
fle:
  call fcmp
  ld bc,1
  ret c
  ret z
  dec c
  ret
MoreOrEqual:
  bit floatmode,(iy+ModeFlags2)
  jr nz,fge
  call MoreLogic
  xor a
  sbc hl,bc
  ccf
  ld b,a
  adc a,a
  ld c,a
  ret
fge:
  call fcmp
  ld bc,0
  ret c
  inc c
  ret
NotEqual:
  bit floatmode,(iy+ModeFlags2)
  jr nz,fne
  call MoreLogic
  xor a
  sbc hl,bc
  ld b,a
  ld c,a
  ret z
  inc c
  ret
fne:
  call fcmp
  ld bc,0
  ret z
  inc c
  ret

RandToken:
  bit floatmode,(iy+ModeFlags2)
  jr nz,+_
  call prng16
  ld b,h
  ld c,l
  ret
_:
  inc hl
  ld (parsePtr),hl
  ld hl,randSingle
float_stepin_0:
  push hl
  call floatstackpush
  ld b,h
  ld c,l
  pop hl
  ld (next_page_call_address),hl
  jp next_page_call




tanToken:
  bit floatmode,(iy+ModeFlags2)
  jp z,ErrBadToken
  ld hl,tanSingle
  jp float_stepin_1
atanToken:
  bit floatmode,(iy+ModeFlags2)
  jr nz,atan_float
  call ParseFullArg
  push bc
  ld bc,128
  cp 2Bh
  call z,ParseNextFullArg
  pop de
  ld hl,atanbin
  ld (next_page_call_address),hl
  jp next_page_call

atan_float:
  ld hl,atanSingle
  jp float_stepin_1

acosToken:
  bit floatmode,(iy+ModeFlags2)
  jp z,ErrBadToken
  ld hl,acosSingle
  jp float_stepin_1
asinToken:
  bit floatmode,(iy+ModeFlags2)
  jp z,ErrBadToken
  ld hl,asinSingle
  jp float_stepin_1

tanhToken:
  bit floatmode,(iy+ModeFlags2)
  jp z,ErrBadToken
  ld hl,tanhSingle
  jp float_stepin_1
coshToken:
  bit floatmode,(iy+ModeFlags2)
  jp z,ErrBadToken
  ld hl,coshSingle
  jp float_stepin_1
sinhToken:
  bit floatmode,(iy+ModeFlags2)
  jp z,ErrBadToken
  ld hl,sinhSingle
  jp float_stepin_1
atanhToken:
  bit floatmode,(iy+ModeFlags2)
  jp z,ErrBadToken
  ld hl,atanhSingle
  jp float_stepin_1
acoshToken:
  bit floatmode,(iy+ModeFlags2)
  jp z,ErrBadToken
  ld hl,acoshSingle
  jp float_stepin_1
asinhToken:
  bit floatmode,(iy+ModeFlags2)
  jp z,ErrBadToken
  ld hl,asinhSingle
  jp float_stepin_1
pow10Token:
  bit floatmode,(iy+ModeFlags2)
  jp z,ErrBadToken
  ld hl,pow10Single
  jp float_stepin_1

meanToken:
  bit floatmode,(iy+ModeFlags2)
  jp z,ErrBadToken
  ld hl,ameanSingle
float_stepin_2:
  push hl
  call ParseFullArg
  pop hl
float_stepin_2_:
  push bc
  push hl
  call ParseNextFullArg
  push bc
  call floatstackpush
  ld b,h
  ld c,l
  pop de
  pop hl
  ld (next_page_call_address),hl
  pop hl
  jp next_page_call
